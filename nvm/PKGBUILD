# Maintainer: Tom Wadley <tom@tomwadley.net>
# Modified for UBOS: Johannes Ernst <jernst@indiecomputing.com>
# * add releasepage
# * added pkgverforked

pkgname=nvm
pkgver=0.35.3
pkgrel=1
pkgdesc="Node Version Manager - Simple bash script to manage multiple active node.js versions"
url="https://github.com/creationix/nvm"
arch=('any')
license=('MIT')
optdepends=('bash: bash completion')
install="${pkgname}.install"
source=("https://github.com/creationix/nvm/archive/v${pkgver}.zip"
        "init-nvm.sh"
        "install-nvm-exec")
sha512sums=('f7505c77e1661e82242c7f6c62d53f6a9bdf62f6b8312cc3ff4de9c8100d95bd7b7d4e370dcca6174361c249c4cd448fac6d4ceb2759bfe68b196b39ddcab1fe'
            'b6a1906f644aa3cab17baeaf9e8bf0b256215b5fa082d73261095f45b5f16769fa91bccc597b22c2cfda0751b0e7f5865e2ff77176bb3e0998966be1fee5a76c'
            '148753d8d76cda4be75139628c04784b7abb70859d3815aa651bfd91a326b8c6b71897c27e7c1482c3255f271057ebc09147cbaba9faa36a66c2e6e2c6d4612a')
releasepage=('https://aur.archlinux.org/cgit/aur.git/tree/?h=nvm')
pkgverforked=('0.35.2-1')

package() {
  cd "${srcdir}"

  # convenience script
  install -Dm644 init-nvm.sh "$pkgdir/usr/share/$pkgname/init-nvm.sh"

  # companion script which installs symlinks in NVM_DIR (see comment in script)
  install -Dm644 install-nvm-exec "$pkgdir/usr/share/$pkgname/install-nvm-exec"

  cd "${pkgname}-${pkgver}"

  # nvm itself
  install -Dm644 nvm.sh "$pkgdir/usr/share/$pkgname/nvm.sh"

  # nvm-exec script for 'nvm exec' command
  install -Dm755 nvm-exec "$pkgdir/usr/share/$pkgname/nvm-exec"

  # bash completion
  install -Dm644 bash_completion "$pkgdir/usr/share/$pkgname/bash_completion"

  # license
  install -Dm644 LICENSE.md "$pkgdir/usr/share/licenses/$pkgname/LICENSE.md"
}

# vim:set ts=2 sw=2 et:
